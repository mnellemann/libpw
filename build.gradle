
apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}


repositories {
    mavenCentral()
}


dependencies {
    implementation("org.slf4j:slf4j-api:2.0.2")

    testImplementation "org.slf4j:slf4j-simple:2.0.1"
    testImplementation('org.spockframework:spock-core:2.2-groovy-3.0')
    testImplementation('com.athaydes:spock-reports:2.3.1-groovy-3.0') {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/mnellemann/libpw")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}


jacoco {
	toolVersion = "0.8.7"
}


jacocoTestReport {
	group = "verification"
	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("${buildDir}/reports/coverage")
	}
}
test.finalizedBy jacocoTestReport


jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.9
			}
		}
	}
}
check.dependsOn jacocoTestCoverageVerification
